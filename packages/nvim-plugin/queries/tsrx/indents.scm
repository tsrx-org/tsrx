; Neovim (nvim-treesitter) indentation rules.
[
  (statement_block "}" @indent.end)
  (component_body "}" @indent.end)
  (class_body "}" @indent.end)
  (switch_body "}" @indent.end)
  (jsx_switch_body "}" @indent.end)
  (jsx_template_block "}" @indent.end)
  (object "}" @indent.end)
  (object_pattern "}" @indent.end)
  (array "]" @indent.end)
  (array_pattern "]" @indent.end)
  (arguments ")" @indent.end)
  (formal_parameters ")" @indent.end)
  (parenthesized_expression ")" @indent.end)
  (jsx_expression "}" @indent.end)
  (style_element ["</style>" "/>"] @indent.end)
  (script_element "</" @indent.end)
  (jsx_statement_container "}" @indent.end)
  (module_body "}" @indent.end)
] @indent.begin

[
  (jsx_element)
  (jsx_fragment)
  (jsx_self_closing_element)
] @indent.begin

((jsx_opening_element) @indent.begin
  (#set! indent.immediate)
  (#set! indent.start_at_same_line))

((jsx_opening_fragment) @indent.begin
  (#set! indent.immediate)
  (#set! indent.start_at_same_line))

(jsx_closing_element ">" @indent.end)
(jsx_closing_fragment ">" @indent.end)
(jsx_self_closing_element "/>" @indent.end)

[
  "}"
  "]"
  ")"
  (jsx_closing_element)
  (jsx_closing_fragment)
] @indent.branch

(style_element ["</style>" "/>"] @indent.branch)
(script_element "</" @indent.branch)

(jsx_self_closing_element "/>" @indent.branch)
